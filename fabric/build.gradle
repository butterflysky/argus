plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'fabric-loom'
}

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    implementation project(":common")
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "21"
}

// Artifact naming: argus-<mod_version>-<mc_version>-fabric.jar
def desiredBase = "${rootProject.archives_base_name}-${rootProject.ext.minecraft_version}"
def desiredVersion = "${project.version}-fabric"

tasks.named("jar") {
    archiveBaseName.set(desiredBase)
    archiveVersion.set(desiredVersion)
    archiveClassifier.set("")
}

tasks.named("remapJar") {
    archiveBaseName.set(desiredBase)
    archiveVersion.set(desiredVersion)
    archiveClassifier.set("")
}

// Disable sources jar in release outputs
tasks.matching { it.name in ["sourcesJar", "remapSourcesJar"] }.configureEach { enabled = false }
