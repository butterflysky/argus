plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.1.6'
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/releases' }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

dependencies {
    implementation project(":common")
    implementation "net.neoforged:neoforge:${rootProject.ext.neo_version}"
}

runs {
    client { }
    server { argument '--nogui' }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Expand mod metadata placeholders
tasks.named("processResources") {
    inputs.properties(
            "mod_version": rootProject.mod_version,
            "minecraft_version": rootProject.ext.minecraft_version,
            "neo_version": rootProject.ext.neo_version
    )
    filesMatching("META-INF/neoforge.mods.toml") {
        expand(
                mod_version: rootProject.mod_version,
                minecraft_version: rootProject.ext.minecraft_version,
                neo_version: rootProject.ext.neo_version
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

// Artifact naming: argus-<mod_version>-<mc_version>-neoforge.jar
tasks.named("jar") {
    archiveBaseName.set("${rootProject.archives_base_name}-${rootProject.ext.minecraft_version}")
    archiveVersion.set("${project.version}-neoforge")
    archiveClassifier.set("")
}

// Disable sources jar in release outputs
tasks.matching { it.name == "sourcesJar" }.configureEach { enabled = false }
